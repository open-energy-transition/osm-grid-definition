// Original Script by Benoit Ribon
[out:json][timeout:1000];

relation["boundary"~"administrative|disputed"]["admin_level"="2"]["ISO3166-1:alpha2"="ES"]->.admin_boundary;
.admin_boundary map_to_area ->.searchArea;

// if you want bbox instead of ISO
// node["place"~"country|state|province"]({{bbox}}) -> .places;
// relation["boundary"="administrative"]["admin_level"="2"](bn.places) -> .admin_boundary;


way["power"~"line|cable"](area.searchArea)->.power_ways;

node(w.power_ways: 1,-1)->.total_line_end;

node.total_line_end(area.searchArea)->.total_inside_country;

(node.total_line_end; - node.total_inside_country;)->.foreign_ends;

way(bn.foreign_ends) -> .cross_ways;

// If you want to add the country borders too then do this and remove the bottom .cross_ways:
// Can be more annoying to see the interconnectors sometimes though!
// (.admin_boundary;.cross_ways;);


.cross_ways;
out meta;
>;
out meta;
